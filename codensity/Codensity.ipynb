{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "{-# LANGUAGE RankNTypes, MultiParamTypeClasses, FlexibleInstances #-}\n",
    "import Prelude hiding (abs)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "-----------------------------------------------------------------------------\n",
    "-- Warmup: Hughes lists\n",
    "-----------------------------------------------------------------------------\n",
    "\n",
    "-- Experienced Haskellers should feel free to skip this section.\n",
    "\n",
    "-- We first consider the problem of left-associative list append.  In\n",
    "-- order to see the difficulty, we will hand-evaluate a lazy language.\n",
    "-- For the sake of being as mechanical as possible, here are the\n",
    "-- operational semantics, where e1, e2 are expressions and x is a\n",
    "-- variable, and e1[e2/x] is replace all instances of x in e1 with e2.\n",
    "--\n",
    "--        e1 ==> e1'\n",
    "--   ---------------------\n",
    "--     e1 e2 ==> e1' e2\n",
    "--\n",
    "--   (\\x -> e1[x]) e2 ==> e1[e2/x]\n",
    "--\n",
    "-- For reference, the definition of append is as follows:\n",
    "--\n",
    "--      a ++ b = foldr (:) b a\n",
    "--\n",
    "-- Assume that, on forcing a saturated foldr, its third argument is\n",
    "-- forced, as follows:\n",
    "--\n",
    "--                e1 ==> e1'\n",
    "--    -----------------------------------\n",
    "--      foldr f e2 e1 ==> foldr f e2 e1'\n",
    "--\n",
    "--  foldr f e2 (x:xs) ==> f x (foldr f e2 xs)\n",
    "--\n",
    "-- Hand evaluate this implementation by forcing the head constructor,\n",
    "-- assuming 'as' is not null:\n",
    "\n",
    "listsample as bs cs = (as ++ bs) ++ cs\n",
    "\n",
    "-- Solution:\n",
    "--\n",
    "--        (as ++ bs) ++ cs\n",
    "--      = foldr (:) cs (as ++ bs)\n",
    "--      = foldr (:) cs (foldr (:) bs as)\n",
    "--      = foldr (:) cs (foldr (:) bs (a:as'))\n",
    "--      = foldr (:) cs (a : foldr (:) b as')\n",
    "--      = a : foldr (:) cs (foldr (:) bs as')\n",
    "--\n",
    "-- Convince yourself that this takes linear time per append, and that\n",
    "-- processing each element of the resulting tail of the list will also\n",
    "-- take linear time.\n",
    "\n",
    "-- We now present Hughes lists:\n",
    "\n",
    "type Hughes a = [a] -> [a]\n",
    "\n",
    "listrep :: Hughes a -> [a]\n",
    "listrep = ($[])\n",
    "\n",
    "append :: Hughes a -> Hughes a -> Hughes a\n",
    "append = (.)\n",
    "\n",
    "-- Now, hand evaluate your implementation on this sample, assuming all\n",
    "-- arguments are saturated.\n",
    "\n",
    "listsample' a b c = listrep (append (append a b) c)\n",
    "\n",
    "-- Solution:\n",
    "--\n",
    "--        listrep (append (append a b) c)\n",
    "--      = (\\l -> l []) (append (append a b) c)\n",
    "--      = (append (append a b) c) []\n",
    "--      = (\\z -> (append a b) (c z)) []\n",
    "--      = (append a b) (c [])\n",
    "--      = (\\z -> a (b z)) (c [])\n",
    "--      = a (b (c []))\n",
    "--\n",
    "-- Convince yourself that the result requires only constant time per\n",
    "-- element, assuming a, b and c are of the form (\\z -> a1:a2:...:z).\n",
    "-- Notice the left-associativity has been converted into\n",
    "-- right-associative function application.\n",
    "\n",
    "-- The codensity transformation operates on similar principles.  This\n",
    "-- ends the warmup."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "-----------------------------------------------------------------------------\n",
    "-- Case for leafy trees\n",
    "-----------------------------------------------------------------------------\n",
    "\n",
    "-- Some simple definitions of trees\n",
    "\n",
    "data Tree a = Leaf a | Node (Tree a) (Tree a)\n",
    "\n",
    "-- Here is the obvious monad definition for trees, where each leaf\n",
    "-- is substituted with a new tree.\n",
    "\n",
    "instance Functor Tree where\n",
    "    fmap f tree = case tree of\n",
    "        Leaf v -> Leaf (f v)\n",
    "        Node l r -> Node (fmap f l) (fmap f r)\n",
    "\n",
    "instance Applicative Tree where\n",
    "    pure = Leaf\n",
    "    Leaf f <*> a = fmap f a\n",
    "    Node l r <*> a = Node (l <*> a) (r <*> a)\n",
    "\n",
    "instance Monad Tree where\n",
    "    return = Leaf\n",
    "    Leaf a   >>= f = f a\n",
    "    Node l r >>= f = Node (l >>= f) (r >>= f)\n",
    "\n",
    "-- You should convince yourself of the performance problem with this\n",
    "-- code by considering what happens if you force it to normal form.\n",
    "\n",
    "sample = (Leaf 0 >>= f) >>= f\n",
    "    where f n = Node (Leaf (n + 1)) (Leaf (n + 1))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [],
   "source": [
    "-- Let's fix this problem.  Now abstract over the /leaves/ of the tree\n",
    "\n",
    "newtype CTree a = CTree { unCTree :: forall r. (a -> Tree r) -> Tree r }\n",
    "\n",
    "-- Please write functions which witness the isomorphism between the\n",
    "-- abstract and concrete versions of trees.\n",
    "\n",
    "treerep :: Tree a -> CTree a\n",
    "treerep tree = CTree $ \\cont -> cont <$> tree >>= id\n",
    "\n",
    "treeabs :: CTree a -> Tree a\n",
    "treeabs ctree = unCTree ctree pure"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [],
   "source": [
    "-- How do you construct a node in the case of the abstract version?\n",
    "-- It is trivial for concrete trees.\n",
    "\n",
    "class Monad m => TreeLike m where\n",
    "    node :: m a -> m a -> m a\n",
    "    leaf :: a -> m a\n",
    "    leaf = return\n",
    "\n",
    "instance TreeLike Tree where\n",
    "    node = Node\n",
    "\n",
    "instance TreeLike CTree where\n",
    "    node (CTree l) (CTree r) = CTree $ \\a -> Node (l a) (r a)\n",
    "\n",
    "-- As they are isomorphic, the monad instance carries over too.  Don't\n",
    "-- use rep/abs in your implementation.\n",
    "\n",
    "instance Functor CTree where\n",
    "    fmap f (CTree ctree) = CTree $ \\cont -> ctree (cont . f)\n",
    "\n",
    "instance Applicative CTree where\n",
    "    pure a = CTree $ \\cont -> cont a\n",
    "    CTree f <*> CTree a = CTree $\n",
    "        \\cont -> f $\n",
    "        \\f' -> a $\n",
    "        \\a' -> cont (f' a')\n",
    "\n",
    "instance Monad CTree where\n",
    "    return = pure\n",
    "    CTree a >>= f = CTree $ \\cont -> a $ \\a' -> unCTree (f a') cont\n",
    "\n",
    "-- We now gain efficiency by operating on the /abstracted/ version as\n",
    "-- opposed to the ordinary one.\n",
    "\n",
    "treeimprove :: (forall m. TreeLike m => m a) -> Tree a\n",
    "treeimprove m = treeabs m\n",
    "\n",
    "-- You should convince yourself of the efficiency of this code.\n",
    "-- Remember that expressions inside lambda abstraction don't evaluate\n",
    "-- until the lambda is applied.\n",
    "\n",
    "sample' = treeabs ((leaf 0 >>= f) >>= f)\n",
    "    where f n = node (leaf (n + 1)) (leaf (n + 1))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [],
   "source": [
    "-----------------------------------------------------------------------------\n",
    "-- General case\n",
    "-----------------------------------------------------------------------------\n",
    "\n",
    "-- Basic properties about free monads\n",
    "\n",
    "data Free f a = Return a | Wrap (f (Free f a))\n",
    "\n",
    "instance Functor f => Functor (Free f) where\n",
    "    fmap f free = case free of\n",
    "        Return a -> Return (f a)\n",
    "        Wrap fa  -> Wrap (fmap (fmap f) fa)\n",
    "\n",
    "instance Functor f => Applicative (Free f) where\n",
    "    pure = Return\n",
    "    f <*> a = case f of\n",
    "        Return f' -> fmap f' a\n",
    "        Wrap ff   -> Wrap $ fmap (<*> a) ff\n",
    "\n",
    "instance Functor f => Monad (Free f) where\n",
    "    return = pure\n",
    "    a >>= f = case a of\n",
    "        Return a' -> f a'\n",
    "        Wrap fa -> Wrap $ fmap (>>= f) fa\n",
    "\n",
    "-- Leafy trees are a special case, with F as the functor. Please write\n",
    "-- functions which witness this isomorphism.\n",
    "\n",
    "data F a = N a a\n",
    "\n",
    "freeFToTree :: Free F a -> Tree a\n",
    "freeFToTree free = case free of\n",
    "    Return a -> Leaf a\n",
    "    Wrap (N l r) -> Node (freeFToTree l) (freeFToTree r)\n",
    "\n",
    "treeToFreeF :: Tree a -> Free F a\n",
    "treeToFreeF tree = case tree of\n",
    "    Leaf a -> Return a\n",
    "    Node l r -> Wrap $ N (treeToFreeF l) (treeToFreeF r)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [],
   "source": [
    "-- We now define an abstract version of arbitrary monads, analogous to\n",
    "-- abstracted trees.  Witness an isomorphism.\n",
    "\n",
    "newtype C m a = C { unC :: forall r. (a -> m r) -> m r }\n",
    "\n",
    "rep :: Monad m => m a -> C m a\n",
    "rep m = C $ \\cont -> cont <$> m >>= id\n",
    "\n",
    "abs :: Monad m => C m a -> m a\n",
    "abs c = unC c pure\n",
    "\n",
    "-- Implement the monad instance from scratch, without rep/abs.\n",
    "\n",
    "instance Functor (C m) where\n",
    "    fmap f (C m) = C $ \\cont -> m $ \\m' -> cont (f m')\n",
    "\n",
    "instance Applicative (C m) where\n",
    "    pure a = C $ \\cont -> cont a\n",
    "    (C f) <*> (C a) = C $ \\cont -> f $ \\f' -> a $ \\a' -> cont (f' a')\n",
    "\n",
    "instance Monad (C m) where\n",
    "    return = pure\n",
    "    C a >>= f  = C $ \\cont -> a $ \\a' -> unC (f a') cont\n",
    "\n",
    "-- By analogy of TreeLike for free monads, this typeclass allows\n",
    "-- the construction of non-Return values.\n",
    "\n",
    "class (Functor f, Monad m) => FreeLike f m where\n",
    "    wrap :: f (m a) -> m a\n",
    "\n",
    "instance Functor f => FreeLike f (Free f) where\n",
    "    wrap = Wrap\n",
    "\n",
    "instance FreeLike f m => FreeLike f (C m) where\n",
    "    -- Toughest one of the bunch. Remember that you have 'wrap' available for the\n",
    "    -- inner type as well as functor and monad instances.\n",
    "    wrap fma = C $ \\cont -> wrap ((`unC` cont) <$> fma)\n",
    "    \n",
    "-- And for our fruits, we now have a fully abstract improver!\n",
    "\n",
    "improve :: Functor f => (forall m. FreeLike f m => m a) -> Free f a\n",
    "improve m = abs m\n",
    "\n",
    "-- Bonus: Why is the universal quantification over 'r' needed?   What if\n",
    "-- we wrote C r m a = ...?  Try copypasting your definitions for that\n",
    "-- case."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Haskell",
   "language": "haskell",
   "name": "haskell"
  },
  "language_info": {
   "codemirror_mode": "ihaskell",
   "file_extension": ".hs",
   "name": "haskell",
   "version": "8.2.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
