{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "import           Control.Monad.Trans.State.Strict\n",
    "import qualified Data.IntMap.Strict  as M\n",
    "import qualified Data.Vector         as V\n",
    "\n",
    "import           Data.Foldable (for_)\n",
    "import           Data.Functor (void)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "graph = M.fromList\n",
    "    [ (1, [2])\n",
    "    , (2, [3])\n",
    "    , (3, [1])\n",
    "    , (4, [2, 3, 5])\n",
    "    , (5, [4, 6])\n",
    "    , (6, [7, 3])\n",
    "    , (7, [6])\n",
    "    , (8, [5, 7, 8])\n",
    "    ]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "data TState = TState\n",
    "    { index    :: Int\n",
    "    , stack    :: [Int]\n",
    "    , stackSet :: V.Vector Bool\n",
    "    , stackTop :: Int\n",
    "    , indices  :: V.Vector (Maybe Int)\n",
    "    , lowlinks :: V.Vector (Maybe Int)\n",
    "    , output   :: [[Int]]\n",
    "    } deriving (Eq, Show)\n",
    "  \n",
    "data Event\n",
    "    = IncrementIndex\n",
    "    | InsertIndex   Int (Maybe Int)\n",
    "    | InsertLowlink Int (Maybe Int)\n",
    "    | Push Int\n",
    "    | Pop\n",
    "    | OutputSCC [Int]\n",
    "    deriving (Eq, Show)\n",
    "\n",
    "data EState = EState\n",
    "    { current :: TState\n",
    "    , history :: [Event]\n",
    "    } deriving (Eq, Show)\n",
    "    \n",
    "whenM :: Monad m => m Bool -> m () -> m ()\n",
    "whenM boolM block = boolM >>= \\b -> if b then block else return ()\n",
    "\n",
    "insert :: V.Vector a -> Int -> a -> V.Vector a\n",
    "insert vector index value = vector V.// [(index, value)]\n",
    "\n",
    "apply :: Event -> TState -> TState\n",
    "apply event state = case event of\n",
    "    IncrementIndex    -> state { index    = index state + 1 }\n",
    "    InsertIndex k v   -> state { indices  = insert (indices  state) k v }\n",
    "    InsertLowlink k v -> state { lowlinks = insert (lowlinks state) k v }\n",
    "    Push i            -> state\n",
    "        { stack    = i : (stack state) \n",
    "        , stackSet = insert (stackSet state) i True\n",
    "        }\n",
    "    Pop               -> let i = head (stack state) in state\n",
    "        { stack    = tail (stack state)\n",
    "        , stackSet = insert (stackSet state) i False\n",
    "        , stackTop = i\n",
    "        }\n",
    "    OutputSCC scc     -> state { output = output state ++ [scc] }\n",
    "\n",
    "initial = TState 0 [] falses undefined nothings nothings []\n",
    "     where \n",
    "        size     = (fst $ M.findMax graph) + 1\n",
    "        falses   = V.replicate size False\n",
    "        nothings = V.replicate size Nothing\n",
    "\n",
    "emit :: Event -> State EState ()\n",
    "emit event = void . modify' $ \\s -> s\n",
    "    { current = apply event (current s) \n",
    "    , history = event : (history s)\n",
    "    }\n",
    "\n",
    "incrementIndex    = emit   IncrementIndex\n",
    "insertIndex   k v = emit $ InsertIndex k v\n",
    "insertLowlink k v = emit $ InsertLowlink k v\n",
    "push i            = emit $ Push i\n",
    "pop               = emit   Pop\n",
    "outputSCC scc     = emit $ OutputSCC scc\n",
    "\n",
    "query accessor i = (V.! i) . accessor <$> gets current"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [],
   "source": [
    "tarjan :: M.IntMap [Int] -> ([[Int]], EState)\n",
    "tarjan graph = flip runState (EState initial []) $ do\n",
    "    for_ (M.keys graph) $ \\v ->\n",
    "        whenM ((Nothing==) <$> query indices v) $\n",
    "            strongConnect v graph\n",
    "    output <$> gets current\n",
    "\n",
    "strongConnect :: Int -> M.IntMap [Int] -> State EState ()\n",
    "strongConnect v graph = do\n",
    "    incrementIndex\n",
    "    i <- index <$> gets current\n",
    "    insertIndex   v (Just i)\n",
    "    insertLowlink v (Just i)\n",
    "    push v\n",
    "    for_ (graph M.! v) $ \\w -> do\n",
    "        i <- query indices w\n",
    "        case i of\n",
    "            Nothing -> do\n",
    "                strongConnect w graph\n",
    "                insertLowlink v =<< (min <$> query lowlinks v <*> query lowlinks w)\n",
    "            Just{}  -> whenM (query stackSet w) $ do\n",
    "                insertLowlink v =<< (min <$> query lowlinks v <*> query indices  w)\n",
    "    whenM ((==) <$> query lowlinks v <*> query indices v) $\n",
    "        outputSCC =<< addSCC v []\n",
    "\n",
    "addSCC :: Int -> [Int] -> State EState [Int]\n",
    "addSCC v scc = do\n",
    "    pop\n",
    "    w <- stackTop <$> gets current\n",
    "    let scc' = w:scc\n",
    "    if w == v\n",
    "        then return scc'\n",
    "        else addSCC v scc'"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "IncrementIndex\n",
       "InsertIndex 1 (Just 1)\n",
       "InsertLowlink 1 (Just 1)\n",
       "Push 1\n",
       "IncrementIndex\n",
       "InsertIndex 2 (Just 2)\n",
       "InsertLowlink 2 (Just 2)\n",
       "Push 2\n",
       "IncrementIndex\n",
       "InsertIndex 3 (Just 3)\n",
       "InsertLowlink 3 (Just 3)\n",
       "Push 3\n",
       "InsertLowlink 3 (Just 1)\n",
       "InsertLowlink 2 (Just 1)\n",
       "InsertLowlink 1 (Just 1)\n",
       "Pop\n",
       "Pop\n",
       "Pop\n",
       "OutputSCC [1,2,3]\n",
       "IncrementIndex\n",
       "InsertIndex 4 (Just 4)\n",
       "InsertLowlink 4 (Just 4)\n",
       "Push 4\n",
       "IncrementIndex\n",
       "InsertIndex 5 (Just 5)\n",
       "InsertLowlink 5 (Just 5)\n",
       "Push 5\n",
       "InsertLowlink 5 (Just 4)\n",
       "IncrementIndex\n",
       "InsertIndex 6 (Just 6)\n",
       "InsertLowlink 6 (Just 6)\n",
       "Push 6\n",
       "IncrementIndex\n",
       "InsertIndex 7 (Just 7)\n",
       "InsertLowlink 7 (Just 7)\n",
       "Push 7\n",
       "InsertLowlink 7 (Just 6)\n",
       "InsertLowlink 6 (Just 6)\n",
       "Pop\n",
       "Pop\n",
       "OutputSCC [6,7]\n",
       "InsertLowlink 5 (Just 4)\n",
       "InsertLowlink 4 (Just 4)\n",
       "Pop\n",
       "Pop\n",
       "OutputSCC [4,5]\n",
       "IncrementIndex\n",
       "InsertIndex 8 (Just 8)\n",
       "InsertLowlink 8 (Just 8)\n",
       "Push 8\n",
       "InsertLowlink 8 (Just 8)\n",
       "Pop\n",
       "OutputSCC [8]"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "complete = tarjan graph\n",
    "void $ traverse print (reverse . history . snd $ complete)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Haskell",
   "language": "haskell",
   "name": "haskell"
  },
  "language_info": {
   "codemirror_mode": "ihaskell",
   "file_extension": ".hs",
   "name": "haskell",
   "version": "8.0.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
