{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "import           Control.Monad.Trans.State.Strict\n",
    "import qualified Data.IntMap.Strict  as M\n",
    "import qualified Data.Vector         as V\n",
    "\n",
    "import           Control.Monad (forM_, when)\n",
    "import           Data.Functor (void)\n",
    "import           Data.Maybe (isNothing, fromJust)\n",
    "import           Prelude hiding (lookup)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "graph = M.fromList\n",
    "    [ (1, [2])\n",
    "    , (2, [3])\n",
    "    , (3, [1])\n",
    "    , (4, [2, 3, 5])\n",
    "    , (5, [4, 6])\n",
    "    , (6, [7, 3])\n",
    "    , (7, [6])\n",
    "    , (8, [5, 7, 8])\n",
    "    ]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [],
   "source": [
    "data TState = TState\n",
    "    { index    :: Int\n",
    "    , stack    :: [Int]\n",
    "    , stackSet :: V.Vector Bool\n",
    "    , stackTop :: Maybe Int\n",
    "    , indices  :: V.Vector (Maybe Int)\n",
    "    , lowlinks :: V.Vector (Maybe Int)\n",
    "    , output   :: [[Int]]\n",
    "    } deriving (Eq, Show)\n",
    "  \n",
    "data Event\n",
    "    = IncrementIndex\n",
    "    | InsertIndex   Int Int\n",
    "    | InsertLowlink Int Int\n",
    "    | Push Int\n",
    "    | Pop\n",
    "    | OutputSCC [Int]\n",
    "    deriving (Eq, Show)\n",
    "\n",
    "data EState = EState\n",
    "    { current :: TState\n",
    "    , history :: [Event]\n",
    "    } deriving (Eq, Show)\n",
    "\n",
    "insert :: V.Vector a -> Int -> a -> V.Vector a\n",
    "insert vector index value = vector V.// [(index, value)]\n",
    "\n",
    "apply :: Event -> TState -> TState\n",
    "apply event state = case event of\n",
    "    IncrementIndex    -> state { index    = index state + 1}\n",
    "    InsertIndex k v   -> state { indices  = insert (indices  state) k (Just v) }\n",
    "    InsertLowlink k v -> state { lowlinks = insert (lowlinks state) k (Just v) }\n",
    "    Push i            -> state\n",
    "        { stack    = i : (stack state) \n",
    "        , stackSet = insert (stackSet state) i True\n",
    "        }\n",
    "    Pop               -> let i = head (stack state) in state\n",
    "        { stack    = tail (stack state)\n",
    "        , stackSet = insert (stackSet state) i False\n",
    "        , stackTop = Just i\n",
    "        }\n",
    "    OutputSCC scc     -> state { output = output state ++ [scc] }\n",
    "\n",
    "initial = TState 0 [] newVector' Nothing newVector newVector []\n",
    "     where \n",
    "        size = (fst $ M.findMax graph) + 1\n",
    "        newVector  = V.replicate size Nothing\n",
    "        newVector' = V.replicate size False\n",
    "\n",
    "emit :: Event -> State EState ()\n",
    "emit event = void . modify' $ \\s -> s\n",
    "    { current = apply event (current s) \n",
    "    , history = event : (history s)\n",
    "    }\n",
    "\n",
    "incrementIndex    = emit   IncrementIndex\n",
    "insertIndex   k v = emit $ InsertIndex k v\n",
    "insertLowlink k v = emit $ InsertLowlink k v\n",
    "push i            = emit $ Push i\n",
    "pop               = emit   Pop\n",
    "outputSCC scc     = emit $ OutputSCC scc"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [],
   "source": [
    "tarjan :: M.IntMap [Int] -> ([[Int]], EState)\n",
    "tarjan graph = flip runState (EState initial []) $ do\n",
    "    forM_ (M.keys graph) $ \\v -> do\n",
    "        is <- indices <$> gets current\n",
    "        when (isNothing (is V.! v)) $ strongConnect v graph\n",
    "    output <$> gets current\n",
    "\n",
    "strongConnect :: Int -> M.IntMap [Int] -> State EState ()\n",
    "strongConnect v graph = do\n",
    "    incrementIndex\n",
    "    i <- index <$> gets current\n",
    "    insertIndex   v i\n",
    "    insertLowlink v i\n",
    "    push v\n",
    "    forM_ (graph M.! v) $ \\w -> do\n",
    "        is <- indices <$> gets current\n",
    "        case is V.! w of\n",
    "            Nothing -> do\n",
    "                strongConnect w graph\n",
    "                ls <- lowlinks <$> gets current\n",
    "                let vLowlink = fromJust (ls V.! v)\n",
    "                let wLowlink = fromJust (ls V.! w)\n",
    "                insertLowlink v (min vLowlink wLowlink)\n",
    "            Just wIndex -> do\n",
    "                sSet <- stackSet <$> gets current\n",
    "                when (sSet V.! w) $ do\n",
    "                    ls <- lowlinks <$> gets current\n",
    "                    let vLowlink = fromJust (ls V.! v)\n",
    "                    insertLowlink v (min vLowlink wIndex)\n",
    "    s <- gets current\n",
    "    let vLowlink = fromJust (lowlinks s V.! v)\n",
    "    let vIndex   = fromJust (indices  s V.! v)\n",
    "    when (vLowlink == vIndex) $ do\n",
    "        scc <- addSCC v []\n",
    "        outputSCC scc\n",
    "\n",
    "addSCC :: Int -> [Int] -> State EState [Int]\n",
    "addSCC v scc = do\n",
    "    pop\n",
    "    w <- fromJust . stackTop <$> gets current\n",
    "    let scc' = w:scc\n",
    "    if w == v\n",
    "        then return scc'\n",
    "        else addSCC v scc'"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "IncrementIndex\n",
       "InsertIndex 1 1\n",
       "InsertLowlink 1 1\n",
       "Push 1\n",
       "IncrementIndex\n",
       "InsertIndex 2 2\n",
       "InsertLowlink 2 2\n",
       "Push 2\n",
       "IncrementIndex\n",
       "InsertIndex 3 3\n",
       "InsertLowlink 3 3\n",
       "Push 3\n",
       "InsertLowlink 3 1\n",
       "InsertLowlink 2 1\n",
       "InsertLowlink 1 1\n",
       "Pop\n",
       "Pop\n",
       "Pop\n",
       "OutputSCC [1,2,3]\n",
       "IncrementIndex\n",
       "InsertIndex 4 4\n",
       "InsertLowlink 4 4\n",
       "Push 4\n",
       "IncrementIndex\n",
       "InsertIndex 5 5\n",
       "InsertLowlink 5 5\n",
       "Push 5\n",
       "InsertLowlink 5 4\n",
       "IncrementIndex\n",
       "InsertIndex 6 6\n",
       "InsertLowlink 6 6\n",
       "Push 6\n",
       "IncrementIndex\n",
       "InsertIndex 7 7\n",
       "InsertLowlink 7 7\n",
       "Push 7\n",
       "InsertLowlink 7 6\n",
       "InsertLowlink 6 6\n",
       "Pop\n",
       "Pop\n",
       "OutputSCC [6,7]\n",
       "InsertLowlink 5 4\n",
       "InsertLowlink 4 4\n",
       "Pop\n",
       "Pop\n",
       "OutputSCC [4,5]\n",
       "IncrementIndex\n",
       "InsertIndex 8 8\n",
       "InsertLowlink 8 8\n",
       "Push 8\n",
       "InsertLowlink 8 8\n",
       "Pop\n",
       "OutputSCC [8]"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "complete = tarjan graph\n",
    "void $ traverse print (reverse . history . snd $ complete)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Haskell",
   "language": "haskell",
   "name": "haskell"
  },
  "language_info": {
   "codemirror_mode": "ihaskell",
   "file_extension": ".hs",
   "name": "haskell",
   "version": "8.0.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
